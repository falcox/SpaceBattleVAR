#define AMBIENT_SPACESHIP_NUM 10
#define TIMER_MSEC_TICK 10
#define FRAMERATE 60

//#define DEBUGGING


/* Include s3d symbols */
#include <Script3d.h>
#include <Camera.s3d.h>
#include <MeshFactory.s3d>
#include <Networker.s3d>
#include <UserInput.s3d>
#include <Xwing.s3d>
#include <TieFighter.s3d>
#include <Ywing.s3d>
#include <Arc170.s3d>
#include <MillenniumFalcon.s3d>
#include <EmperorShuttle.s3d>
#include <StarDestroyer.s3d>
#include <DeathStar.s3d>
#include <EscapePod.s3d>
#include <SkyBox.s3d>
#include <Radar.s3d>
#include <CollisionDetector.s3d>
#include <AmbientEngine.s3d>

/* Set global scene parameters */
SET SCENE_FOV  = 90;
SET SCENE_NEAR = 0.5;
SET SCENE_FAR  = 5000;

var myFighter;
var networkController =  void;
var collider;
var userInputController;
var ambientController;
var enemiesFighter = Array(ENEMIES_FIGHTER_NUM);
var ambientSpaceship = Array(AMBIENT_SPACESHIP_NUM);
var meshes;
var mySkyBox;
var myRadar;

/* Function declarations */
function DrawGrid(col, size);
function CameraMoveMouse();


function OnDownload()
{
	FileDownload("3d_models/Xwing.zip");
	FileDownload("3d_models/Tie_2.zip");
	FileDownload("3d_models/ywing.zip");
	FileDownload("3d_models/arc170.zip");
	FileDownload("3d_models/millennium_falcon.zip");
	FileDownload("3d_models/emperor_shuttle.zip");
	FileDownload("3d_models/EscapePod.zip");
	FileDownload("3d_models/DeathStar4.zip");
	FileDownload("3d_models/StarDestroyer.zip");
	FileDownload("images/spacebackgrounds.zip");
	FileDownload("images/billboards.zip");	
}

function OnInit(params)
{
#ifdef DEBUGGING
	CameraGetCurrent().SetPosition([20, 0, 0]);
#endif
	networkController = Networker();
	
	userInputController = UserInput();
	
	collider = CollisionDetector(AMBIENT_SPACESHIP_NUM, ENEMIES_FIGHTER_NUM, networkController);
	
	meshes = MeshFactory();
	
	ambientController = AmbientEngine(networkController, meshes);
	
	myFighter = Xwing( meshes );
#ifdef DEBUGGING
	myFighter.useCamera(false);
#else
	myFighter.useCamera(true);
#endif
	myFighter.setPosition([ 50, 100, 0 ]);
	
	mySkyBox = SkyBox();
	mySkyBox.init();
	
	myRadar = Radar();
	
	ambientSpaceship[0] = EmperorShuttle( meshes );
	ambientSpaceship[0].setCenter([200.0, 0, 0]);
	ambientSpaceship[0].setRadius(100);
	ambientSpaceship[0].setOrbitPeriod(20.0);
	ambientSpaceship[0].setServerTime(GetTime());
	
	ambientSpaceship[1] = DeathStar( meshes );
	ambientSpaceship[1].setCenter([2500, 2000, 2500]);
	ambientSpaceship[1].setRadius(0);
	ambientSpaceship[1].setOrbitPeriod(500.0);
	ambientSpaceship[1].setServerTime(GetTime());	
	
	/*---Global XVR parameters---*/
	SetFrameRate(FRAMERATE);	
	SetTimeStep(TIMER_MSEC_TICK);

	Outputln("START!");
}

function OnFrame()
{
	
	SceneBegin();
#ifdef DEBUGGING
	CameraMoveMouse();
#endif
	DrawGrid([0.5, 0.5, 0.5], 10000);

	/*---here we draw our fighter---*/
	mySkyBox.draw();
	myFighter.draw();
	
	/*---here we draw all enemies fighter---*/
	for (var i = 0; i < ENEMIES_FIGHTER_NUM; i++) {
		if (enemiesFighter[i] != void) {
			enemiesFighter[i].draw();
		}
	}
	/*---here we draw all ambient spaceships---*/
	for (var i = 0; i < AMBIENT_SPACESHIP_NUM; i++) {
		if (ambientSpaceship[i] != void) {
			ambientSpaceship[i].draw();
		}
	}
	
	myRadar.draw();
	
	/*if (ambientSpaceship[0] != void) {
		var pos = ambientSpaceship[0].object.getPosition();
		ConsoleText(0.05,0.9,"Ambient[0] ["+str(pos.x)+","+str(pos.y)+","+str(pos.z)+"]");
	}*/
	
	SceneEnd();
}

function DownloadReady(RequestID)
{
	// TODO
}

function OnTimer()
{
	userInputController.userControlFighter(myFighter);
	if (networkController != void) {
		networkController.poll();
		networkController.sendFighterData(myFighter.getFighterData());
	}
	
	ambientController.tick(ambientSpaceship);
	
	myFighter.update();
	
	for (var i = 0; i < ENEMIES_FIGHTER_NUM; i++) {
		if (enemiesFighter[i] != void) {
			enemiesFighter[i].update();
		}
	}
	for (var i = 0; i < AMBIENT_SPACESHIP_NUM; i++) {
		if (ambientSpaceship[i] != void) {
			ambientSpaceship[i].update();
		}
	}
	
	myRadar.update(myFighter.getPosition(), ambientSpaceship, enemiesFighter);
	
	collider.checkCollisionBBox(myFighter, enemiesFighter, ambientSpaceship);
	
	// TEMPORANEO! rimuovo gli elementi ambiente distrutti
	for (var i = 0; i < AMBIENT_SPACESHIP_NUM; i++) {
		if (ambientSpaceship[i] != void) {
			if (ambientSpaceship[i].isDestroyed()) {
				ambientSpaceship[i] = void;
			}
		}
	}
	
	mySkyBox.update(myFighter.getPosition(), myFighter.getAngles()[0]);
	myRadar.update(myFighter, ambientSpaceship, enemiesFighter);

}

function OnEvent(eventID, wparam, lparam){
	if (networkController != void) {
		if (eventId == networkController.isIdEvent()) {
			ambientController.start();
			Outputln(Sprintf("GET ID: %d", wparam));
		}
		if (eventId == networkController.isEnemyDataEvent()) {
			var id = wparam[0];
			var type = wparam[1];
			if (enemiesFighter[id] == void) {
				/* New player has joined the game! Share ambient object! */
				ambientController.advertise(ambientSpaceship);
				if (type == XWING_TYPE) { /* xwing */
					enemiesFighter[id] = Xwing( meshes );
				} else if (type == TIE_TYPE) { /* tie */
					enemiesFighter[id] = TieFighter( meshes );
				} else if (type == YWING_TYPE) { /* ywing */
					enemiesFighter[id] = Ywing( meshes );
				} else if (type == ARC170_TYPE) { /* arc170 */
					enemiesFighter[id] = Arc170( meshes );
				} else if (type == MILLFALC_TYPE) { /* millennium falcon */
					enemiesFighter[id] = MillenniumFalcon( meshes );
				} else {
					Outputln("Error! fighter type does not exists!");
					return;
				}
			}
			enemiesFighter[id].setPosition([wparam[2], wparam[3], wparam[4]]);
			enemiesFighter[id].setAngle(wparam[5], wparam[6], wparam[7]);
			enemiesFighter[id].setSpeed(wparam[8]);
			enemiesFighter[id].setAnimationFrame(wparam[9]);
		}
		if (eventId == networkController.isEnemyExitEvent()) {
			enemiesFighter[wparam] = void;
			Outputln(Sprintf("Player %d is gone!",wparam));
		}
		if (eventId == networkController.isAmbientDestroyEvent()) {
			ambientSpaceship[wparam] = void;
			Outputln(Sprintf("Ambient %d is gone!",wparam));
		}
		if (eventId == networkController.isAmbientDataEvent()) {
			var id = wparam[0];
			var type = wparam[1];
			if (ambientSpaceship[id] == void) {
				if (type == ESCAPE_POD) { 
					ambientSpaceship[id] = EscapePod( meshes );
				} else if (type == EMPEROR_SHUTTLE) { 
					ambientSpaceship[id] = EmperorShuttle( meshes );
				} else if (type == STAR_DESTROYER) { 
					ambientSpaceship[id] = StarDestroyer( meshes );
				} else {
					Outputln("Error! Ambient type does not exists!");
					return;
				}
			}
			ambientSpaceship[id].setCenter([ wparam[2], wparam[3], wparam[4] ]);
			ambientSpaceship[id].setRadius(wparam[5]);
			ambientSpaceship[id].setOrbitPeriod(wparam[6]);
			ambientSpaceship[id].setServerTime(wparam[7]);
		}
	}
}

function OnError(){
	// TODO: put your errors handling code here
}

function OnExit()
{
	// TODO: put your cleanup code here
	EraseCache();
}

function DrawGrid(col, size)
{
	/* let's not mess up current OpenGL status */
	glPushAttrib(GL_LIGHTING_BIT | GL_LINE_BIT | GL_CURRENT_BIT);
	
	glLineWidth(1);
	glDisable(GL_LIGHTING);
	glColor(col);
	var max = size / 2.0;
	var min = -max;	
	var step = size / 100.0;
	
	glBegin(GL_LINES);
		for (var i = min; i <= max; i += step)
		{
			glVertex(i, 0, max);
			glVertex(i, 0, min);
			
			glVertex(max, 0, i);
			glVertex(min, 0, i);
		}
	glEnd();
	
	/* polite restoration of previous OpenGL status */
	glPopAttrib();
}
